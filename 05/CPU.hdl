// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    Mux16(a=instruction, b=poutM, sel=instruction[15], out=w1);
    
    Not(in=instruction[15], out=notinst15);
    Or(a=instruction[5], b=notinst15, out=flgA);
    
    ARegister(in=w1, load=flgA, out[0..14]=addressM, out=A);
    
    Mux16(a=A, b=inM, sel=instruction[12] , out=AM);
    
    And(a=instruction[4], b=instruction[15], out=flgD);

    DRegister(in=poutM, load=flgD, out=D);

    ALU(x=D, y=AM, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], zr=flgzr, ng=flgng, out=outM, out=poutM );
    
    // Out>0
    Or(a=flgzr, b=flgng, out=tmp0);
    Not(in=tmp0, out=tmp1);
    And(a=instruction[0], b=tmp1, out=flgpos);
    
    //Out==0
    And(a=instruction[1], b=flgzr, out=flgeqz);
    
    //out neq 0
    Not(in=flgzr, out=flgnzr);
    And(a=instruction[0], b=instruction[2], out=tmp2);
    And(a=tmp2, b=flgnzr, out=flgnonzero);
    
    //out < 0
    And(a=flgnzr, b=flgng, out=tmp3);
    And(a=instruction[2], b=tmp3, out=flgnegzr);
    
    Or8Way(in[0]=flgpos, in[1]=flgeqz, in[2]=flgnonzero, in[3]=flgnegzr, in[4..7]=false, out=flgjump);
    
    And(a=flgjump, b=instruction[15], out=flgjump2);
    
    Not(in=flgjump2, out=flginc);
    
    PC(in=A, reset=reset, load=flgjump2, inc=flginc , out[0..14]=pc);
    
    And(a=instruction[3], b=instruction[15], out=writeM);
}
